buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.3.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.github.kt3k.coveralls'

group            = 'moe.pine'
version          = '0.0.6'
archivesBaseName = 'rxkotlin-collections'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "io.reactivex:rxjava:1.1.1"
    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

jacoco {
    toolVersion = '0.7.6.201602180812'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

publish {
    userOrg        = 'pinemz'
    groupId        = 'moe.pine'
    artifactId     = 'rxkotlin-collections'
    publishVersion = project.version
    desc           = 'Kotlin Collections Methods for RxJava'
    website        = 'https://github.com/pine613/rxkotlin-collections'
    licences       = ["MIT"]
    dryRun         = false
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

task findConvensions << {
    println project.getConvention()
}

check.dependsOn jacocoTestReport